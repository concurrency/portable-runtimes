#INCLUDE "plumbing.module"

--this process fades in and out, the led attached to "pin"
PROC led.fadeInOut (VAL INT pin, millisFadeIn, millisFadeOut, CHAN SIGNAL in?)
  WHILE TRUE
    SEQ
      in ? SIGNAL
      SEQ i = 0 FOR 100 
        SEQ
          analogWrite(pin, (BYTE i))
          delay(millisFadeIn)
      SEQ i = 100 FOR 100 STEP (- 1)
        SEQ
          analogWrite(pin, (BYTE i))
          delay(millisFadeOut)
      SEQ
        analogWrite(pin, 0)
:

--this process receives the signal from "buttonPress" and then sends a delayed signal to each led.
PROC relay.delay (VAL INT period, CHAN SIGNAL in?, CHAN SIGNAL out3!,out5!,out6!,out9!,out10!,out11!)
  WHILE TRUE
    SEQ
      in ? SIGNAL
      SEQ
        out3 ! SIGNAL
        delay(period)
        out5 ! SIGNAL
        delay(period + 17)
        out6 ! SIGNAL
        delay(period + 32)
        out9 ! SIGNAL
        delay(period + 45)
        out10 ! SIGNAL
        delay(period + 56) 
        out11 ! SIGNAL
:

--this process initializes multiple pins to analog mode.
PROC beginAnalog.multiPin(VAL INT pinA,pinB,pinC,pinD,pinE,pinF)
  PAR
    beginAnalog(pinA)
    beginAnalog(pinB)
    beginAnalog(pinC)
    beginAnalog(pinD)
    beginAnalog(pinE)
    beginAnalog(pinF)
:



PROC main()
  CHAN SIGNAL start,r.d3,r.d5,r.d6,r.d9,r.d10,r.d11:
  WHILE TRUE
    PAR
      beginAnalog.multiPin(3,5,6,9,10,11)
      button.press(2, start!)
      relay.delay(25, start?,r.d3!,r.d5!,r.d6!,r.d9!,r.d10!,r.d11!)
      led.fadeInOut(3,7,10,r.d3?)
      led.fadeInOut(5,7,10,r.d5?)
      led.fadeInOut(6,7,10,r.d6?)
      led.fadeInOut(9,7,10,r.d9?)
      led.fadeInOut(10,7,10,r.d10?)
      led.fadeInOut(11,7,10,r.d11?)
      
:
