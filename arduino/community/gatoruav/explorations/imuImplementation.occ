#INCLUDE "imu.module"
VAL INT X IS 0:
VAL INT Y IS 1:

PROC display([]CHAN INT theta?)
  [2]INT thetas:
  WHILE TRUE
    SEQ
      theta[X] ? thetas[X]
      theta[Y] ? thetas[Y]

      serialWriteDecInt(thetas[X])
      serial.write.string("     ")
      serialWriteDecInt(thetas[Y])
      serial.write.string("*n")
:

-- This runs at around 143 reading per second
PROC main()
  
  [2]CHAN INT gyro.out, accel.out, accel.conv, gyro.smooth, gyro.degree, theta:
  [2]CHAN SIGNAL sig.gyro, sig.accel:
  PAR

    ping(sig.gyro[X]!)
    ping(sig.gyro[Y]!)
    ping(sig.accel[X]!)
    ping(sig.accel[Y]!)
    -- Gyro adcs
    adc(1, sig.gyro[X]?, gyro.out[X]!)
    adc(A2, sig.gyro[Y]?, gyro.out[Y]!)
    -- Accel adcs
    adc(A3, sig.accel[X]?, accel.out[X]!)
    adc(A4, sig.accel[Y]?, accel.out[Y]!)
  
    accel.conv.y(accel.out[Y]?, accel.conv[Y]!)
    accel.conv.x(accel.out[X]?, accel.conv[X]!)

    gyro.filter(gyro.out[Y]?, gyro.smooth[Y]!)
    gyro.filter(gyro.out[X]?, gyro.smooth[X]!)

    gyro.to.degrees(gyro.smooth[Y]?, gyro.degree[Y]!)
    gyro.to.degrees(gyro.smooth[X]?, gyro.degree[X]!)

    comp.filter(gyro.degree[Y]?, accel.conv[Y]?, theta[Y]!, 95, 1)
    comp.filter(gyro.degree[X]?, accel.conv[X]?, theta[X]!, 95, 1)

    -- Comment/Uncomment to show/not show timing info
    --time.int(theta[X]?, 'X')
    --time.int(theta[Y]?, 'Y')
    -- Comment/Uncomment to show/not show angles
    display(theta?)
:
