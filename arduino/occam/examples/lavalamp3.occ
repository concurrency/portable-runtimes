#INCLUDE "plumbing.module"

PROC pwm.cycle (VAL INT pin, millis)
  SEQ
    SEQ i = 0 FOR 255
      SEQ
        analogWrite(pin, (BYTE i))
        delay(millis)
    SEQ i = 255 FOR 255 STEP (- 1)
      SEQ
        analogWrite(pin, (BYTE i))
        delay(millis)
:


PROC fader (VAL INT pin, CHAN SIGNAL req!, CHAN INT adc?)
  INITIAL INT step IS 1:
  SEQ
    beginAnalog(pin)
    WHILE TRUE
      INT reading, millis:
      SEQ
        -- Request a reading
        req ! SIGNAL
        -- Get the value
        adc ? reading
        -- Calculate the delay
        millis := (reading / 200) + pin
        -- Fade with a delay of 0 - 5ms + (pin)ms
        pwm.cycle(pin, millis)
:

PROC sema ([3]CHAN SIGNAL ses?, [3]CHAN INT readings!, 
             CHAN SIGNAL s, CHAN INT v)
  WHILE TRUE
    ALT i = 0 FOR 3
      INT reading:
      ses[i] ? SIGNAL
        SEQ
          s ! SIGNAL
          v ? reading
          readings[i] ! reading
:
      
PROC show.int (CHAN INT in?, out!)
  INT tmp:
  WHILE TRUE
    SEQ
      in ? tmp
      serialWriteInt(tmp)
      serialWrite("*n")
      out ! tmp
:

VAL []INT PINS IS [3, 6, 11]:
PROC main ()
  CHAN SIGNAL s:
  CHAN INT v:
  [3]CHAN SIGNAL ses:
  [3]CHAN INT readings:
  PAR
    adc (2, s?, v!)
    sema (ses?, readings!, s!, v?)
    PAR i = 0 FOR 3
      fader (PINS[i], ses[i]!, readings[i]?)
:
        
